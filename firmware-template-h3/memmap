MEMORY
{
	SRAMA1 : ORIGIN = 0x00000000, LENGTH = 64K
	SRAMA2 : ORIGIN = 0x00044000, LENGTH = 32K
    ram    : ORIGIN = 0x40000000, LENGTH = 16M  
}

__ram_start = ORIGIN(ram);
__ram_size  = LENGTH(ram);
__ram_end   = __ram_start + __ram_size;

__stack_start = __ram_start + 0x300000;
__heap_start = __ram_start + 0x500000;

__und_stack_size = 0x08000;
__abt_stack_size = 0x08000;
__fiq_stack_size = 0x08000;
__irq_stack_size = 0x08000;
__svc_stack_size = 0x80000;
__sys_stack_size = 0x08000;

__svc_cpus_stack_size = 0x10000;

SECTIONS
{
  .srama1 :
  {
	. = ALIGN(4);
    *(.srama1)
    *(.srama1*)
  } >SRAMA1 
  
  .srama2 :
  {
	. = ALIGN(4);
    *(.srama2)
    *(.srama2*)
  } >SRAMA2 

    .init : 
    {
    . = ALIGN(4);
    KEEP(*(.init))
    } > ram
    
    .text : 
    {
    . = ALIGN(4);
     *( .text.unlikely*)
     *( .text.hot*)
     *(.text*)
    } > ram
    
    .rodata : 
    {
    . = ALIGN(4);
     *(.rodata*)
    } > ram
    
      .preinit_array :
  {
    PROVIDE_HIDDEN (__preinit_array_start = .);
    KEEP (*(.preinit_array*))
    PROVIDE_HIDDEN (__preinit_array_end = .);
  } > ram

  .init_array :
  {
    PROVIDE_HIDDEN (__init_array_start = .);
    KEEP (*(SORT(.init_array.*)))
    KEEP (*(.init_array*))
    PROVIDE_HIDDEN (__init_array_end = .);
  } > ram

  .fini_array :
  {
    PROVIDE_HIDDEN (__fini_array_start = .);
    KEEP (*(.fini_array*))
    KEEP (*(SORT(.fini_array.*)))
    PROVIDE_HIDDEN (__fini_array_end = .);
  } > ram

    .data : 
    {  
    . = ALIGN(4);
__data_start = . ;    
     *(SORT(.table*));
     *(.data*)
    . = ALIGN(4);
__data_end = . ;      
    } > ram
 
    .bss :
    {
    . = ALIGN(4);
__bss_start = . ;        
     *(.bss*)
     *(COMMON)
    . = ALIGN(4);
__bss_end = . ; 
    } > ram
	
	/DISCARD/ :
	{
	*(*.ARM.attributes)
	*(*.comment)
	*(*.debug*)
	}	
}

PROVIDE(end = .);
_end = .;

. = __stack_start;
. = . + __und_stack_size; 
__und_stack_top = .;

. = ALIGN(4);
. = . + __abt_stack_size; 
__abt_stack_top = .;

. = ALIGN(4);
. = . + __fiq_stack_size; 
__fiq_stack_top = .;

. = ALIGN(4);
. = . + __irq_stack_size; 
__irq_stack_top = .;

. = ALIGN(4);
PROVIDE( stack_low = .);
. = . + __svc_stack_size; 
__svc_stack_top = .;
PROVIDE( _sp = __svc_stack_top ); 

. = ALIGN(4);
. = . + __sys_stack_size; 
__sys_stack_top = .;

. = ALIGN(4);
. = . + __svc_cpus_stack_size; 
__svc_stack_top_core1 = .;

. = ALIGN(4);
. = . + __svc_cpus_stack_size; 
__svc_stack_top_core2 = .;

. = ALIGN(4);
. = . + __svc_cpus_stack_size; 
__svc_stack_top_core3 = .;

. = __heap_start;
 heap_low = .;
 heap_top = __ram_end;
